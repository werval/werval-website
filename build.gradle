
description = "QiWeb Documentation"

apply plugin: 'java'

repositories {
    maven { url qiwebLocalRepository }
    maven {
    	url "https://repo.codeartisans.org/qiweb"
    	credentials { username = "qiweb"; password = "qiweb" }
	}
}


//
//  ____                            _       _   _
// |    \ ___ ___ _ _ _____ ___ ___| |_ ___| |_|_|___ ___
// |  |  | . |  _| | |     | -_|   |  _| .'|  _| | . |   |
// |____/|___|___|___|_|_|_|___|_|_|_| |__,|_| |_|___|_|_|
//

configurations {
    doc_develop
    doc_current
    doc_0_2_0_Alpha
    doc_0_1_3_Alpha
}

dependencies {

    doc_develop "org.qiweb:org.qiweb.runtime:0"
    doc_develop "org.qiweb:org.qiweb.doc:0"
    doc_develop "org.qiweb:org.qiweb.modules.guice:0"
    doc_develop "org.qiweb:org.qiweb.modules.jdbc:0"
    doc_develop "org.qiweb:org.qiweb.modules.jndi:0"
    doc_develop "org.qiweb:org.qiweb.modules.rythm:0"

    doc_current "org.qiweb:org.qiweb.website:0.2.0.Alpha:doc@zip"

    doc_0_2_0_Alpha "org.qiweb:org.qiweb.website:0.2.0.Alpha:doc@zip"
    doc_0_1_3_Alpha "org.qiweb:org.qiweb.website:0.1.3.Alpha:doc@zip"

}

apply plugin:org.qiweb.website.WebSitePlugin
website {
    doc_develop project.configurations.doc_develop
    doc_current project.configurations.doc_current
    doc_versions = [
        '0.2.0.Alpha': project.configurations.doc_0_2_0_Alpha,
        '0.1.3.Alpha': project.configurations.doc_0_1_3_Alpha
    ]
}
// TODO Move this to buildSrc into the Plugin, don't know how yet
sourceSets.main.resources.srcDirs += file('build/generated-src/develop/resources')
sourceSets.main.resources.srcDirs += file('build/generated-src/current/resources')
sourceSets.main.resources.srcDirs += file('build/generated-src/versions/resources')
sourceSets.main.resources.srcDirs += file('build/generated-src/front/resources')


//
//  _____ _ _ _ _     _
// |     |_| | | |___| |_
// |  |  | | | | | -_| . |
// |__  _|_|_____|___|___|
//    |__|
//

buildscript {
    repositories {
        maven { url qiwebLocalRepository }
        maven {
            url "https://repo.codeartisans.org/qiweb"
            credentials { username = "qiweb"; password = "qiweb" }
        }
    }
    dependencies { classpath 'org.qiweb:org.qiweb.gradle:0' }
}

apply plugin:'qiweb'
devshell {
    extraWatch += file( 'src/asciidoc' )
}

dependencies {

    compile 'org.qiweb:org.qiweb.api:0'

    runtime 'org.qiweb:org.qiweb.server.bootstrap:0'
    runtime 'ch.qos.logback:logback-classic:1.1.2'
    
    testCompile 'org.qiweb:org.qiweb.test:0'

}
